DATA:	FILE = D:/multilevel-omega_mplus.csv;

VARIABLE:
NAMES ARE CLASS MPAI1 MPAI2 MPAI3 RI1 RI2;
USEVARIABLES = CLASS MPAI1 MPAI2 MPAI3;

CLUSTER = CLASS;
MISSING = ALL (-999);

ANALYSIS:
TYPE=TWOLEVEL;
MODEL:
%WITHIN%
MATHW BY MPAI1* (WL1)
MPAI2 (WL2)
MPAI3 (WL3);
MATHW@1; 

MPAI1 (WR1);
MPAI2 (WR2);
MPAI3 (WR3);

%BETWEEN%
MATHB BY MPAI1* (BL1)
MPAI2 (BL2)
MPAI3 (BL3);
MATHB@1;

MPAI1 (BR1);
MPAI2 (BR2);
MPAI3 (BR3);


OUTPUT: SAMPSTAT;
MODEL CONSTRAINT: NEW(NUMW DENOMW OMEGAW HW
NUMB DENOMB OMEGAB HB);
NUMW = (WL1+WL2+WL3)**2; 
DENOMW = ((WL1+WL2+WL3)**2)+(WR1+WR2+WR3); 
OMEGAW = NUMW/DENOMW; 
HW = 1/(1+(1/((WL1**2/WR1)+(WL2**2/WR2)+(WL3**2/WR3))));

NUMB = (BL1+BL2+BL3)**2; 
DENOMB = ((BL1+BL2+BL3)**2)+(BR1+BR2+BR3); 
OMEGAB = NUMB/DENOMB; 
HB = 1/(1+(1/((BL1**2/BR1)+(BL2**2/BR2)+(BL3**2/BR3)))); 
WR1 > 0; BR1 > 0;
WR2 > 0; BR2 > 0;
WR3 > 0; BR3 > 0;

