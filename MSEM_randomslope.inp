TITLE: 1-1-1 mediation radomslope(MSEM) 
DATA: FILE = D:/大学资料/休息羞耻所有课题/休息羞耻文章文件夹/MPAI_MSEM.csv; 
VARIABLE: NAMES ARE 
y x m gender age h id time; !y = MPAI/ST, x = RI, m = SA

USEVARIABLES ARE 
id y x m gender age; 
CLUSTER IS id; ! Level-2 grouping identifier 
BETWEEN = gender age ;


ANALYSIS: TYPE IS TWOLEVEL RANDOM;


MODEL: ! model specification follows 
%WITHIN% ! Model for Within effects follows
sa | m ON x; ! regress m on x, call the random slope "sa" 
sb | y ON m; ! regress y on m, call the random slope "sb"
sc | y ON x; ! regress y on x, call the random slope "sc" 
%BETWEEN% ! Model for Between effects follows 
sa sb sc x m y; ! estimate Level-2 (residual) variances for sa, sb, sc, x, m, and y
sa WITH sc x m y; ! estimate Level-2 covariances of sa with sc, x, m, and y
sa WITH sb(cab); ! estimate Level-2 covariance of sa and sb, call it "cab"
sb WITH sc x m y; ! estimate Level-2 covariances of sb with sc, x, m, and y 
sc WITH x m y; ! estimate Level-2 covariances of sc with x, m, and y
m ON x(ab); ! regress m on x, call the slope "ab"; 
!ab = contextual effect, not the Between slope 
y ON m(bb); ! regress y on m, call the slope "bb"; 
!bb = contextual effect, not the Betweeen slope 
y ON x; ! regress y on x 
Y on gender;
Y on age;
[sa](aw); ! estimate the mean of sa, call it "aw" 
[sb](bw); ! estimate the mean of sb, call it "bw" 
MODEL CONSTRAINT: ! section for computing indirect effects 
NEW(a b indb indw); ! name the indirect effects 
a=aw+ab; ! compute Between a path
b=bw+bb; ! compute Between b path
indw=aw*bw+cab; ! compute the Within indirect effect 
indb=a*b; ! compute the Between indirect effect 
OUTPUT: TECH1 TECH8 CINTERVAL;